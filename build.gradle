plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'maven-publish'
}

archivesBaseName = project.name + '-' + project.mcVersion + '-fabric'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

sourceSets.main.java.srcDirs += 'java'
sourceSets.main.resources.srcDirs += 'resources'

repositories {
	maven {
		name = "CurseMaven"
		url = "https://www.cursemaven.com"
	}
	maven {
		name = "Fuzs Mod Resources"
		url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
	}
	maven {
		name = "JourneyMap (Public)"
		url = "https://jm.gserv.me/repository/maven-public/"
	}
	maven {
		name = "Architectury"
		url = "https://maven.architectury.dev/"
		content { includeGroup "dev.architectury" }
	}
	maven {
		name = "FTB Resources"
		url = "https://maven.saps.dev/minecraft"
		content {
			includeGroup "dev.latvian.mods"
			includeGroup "dev.ftb.mods"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.mcVersion}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loaderVersion}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabricVersion}"

	modImplementation "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${forgeConfigApiVersion}"
	modImplementation 'curse.maven:iceberg-539382:3832794'
	modApi 'curse.maven:xaerominimap-263420:3835774'
	modApi 'dev.ftb.mods:ftb-chunks-fabric:1900.3.7-build.154'
	modApi group: 'info.journeymap', name: 'journeymap-api', version: '1.19-1.8-fabric-SNAPSHOT', changing: true
}

loom {
	accessWidenerPath = file("src/main/resources/${project.name.toLowerCase()}.accesswidener")
}

processResources {
	inputs.property 'version', project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	manifest {
		attributes(["Specification-Title":		project.name,
					"Specification-Vendor":		project.author,
					"Specification-Version":	project.version,
					"Implementation-Title":		project.name,
					"Implementation-Version":	project.version,
					"Implementation-Vendor":	project.author,
					"Implementation-Timestamp":	new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
	}
}
