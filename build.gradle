buildscript {
	repositories {
		maven { url = "https://maven.minecraftforge.net" }
		maven { url = "https://plugins.gradle.org/m2/" }
		maven { url = "https://repo.spongepowered.org/maven" }
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'

archivesBaseName = project.name + '-' + project.mcVersion

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
}

mixin {
	add sourceSets.main, "${project.name.toLowerCase()}.refmap.json"
}

minecraft {
	mappings channel: 'official', version: project.mcVersion

	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
	runs {
		client {
			properties 'forge.logging.markers': ''
			properties 'forge.logging.console.level': 'info'
			properties 'terminal.ansi': 'true'
			workingDirectory project.file('run').canonicalPath
			source sourceSets.main

			arg "-mixin.debug=true"
			arg "-mixin.checks.interfaces=true"
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/extractSrg/output.srg"
		}
	}
}

repositories {
	maven {
		name = "CurseMaven"
		url "https://www.cursemaven.com"
	}
	maven {
		name = "JourneyMap (Public)"
		url = "https://jm.gserv.me/repository/maven-public/"
	}
}

sourceSets {
	main {
		resources {
			srcDir 'src/generated/resources'
		}
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${project.mcVersion}-${project.forgeVersion}"
	annotationProcessor 'org.spongepowered:mixin:0.8:processor'

	implementation fg.deobf('curse.maven:iceberg-520110:3788411')
	compileOnly fg.deobf('curse.maven:xaerominimap-263420:3778432')
	compileOnly fg.deobf('curse.maven:ftblibrary-404465:3553840')
	compileOnly fg.deobf('curse.maven:ftbchunks-314906:3603374')
	compileOnly fg.deobf(group: 'info.journeymap', name: 'journeymap-api', version: '1.16.5-1.8-SNAPSHOT', changing: true)
	runtimeOnly fg.deobf('curse.maven:journeymap-32274:3803008')
}

jar {
	manifest {
		attributes(["Specification-Title": project.name,
					"Specification-Vendor": project.author,
					"Specification-Version": project.version,
					"Implementation-Title": project.name,
					"Implementation-Version": project.version,
					"Implementation-Vendor" : project.author,
					"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
					"MixinConfigs": "${project.name.toLowerCase()}.mixins.json"],)
	}
}
